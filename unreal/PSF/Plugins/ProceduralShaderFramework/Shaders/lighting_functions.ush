#ifndef PROCEDURAL_SHADER_FRAMEWORK_LIGHTING_H
#define PROCEDURAL_SHADER_FRAMEWORK_LIGHTING_H

#include "sdf_functions.ush"

//LOCAL HELPERS
struct SunriseLight
{
    float3 sundir;
    float3 earthCenter;
    float earthRadius;
    float atmosphereRadius;
    float sunIntensity;
};

float2 densitiesRM(float3 position, SunriseLight light)
{
    float h = max(0., length(position - light.earthCenter) - light.earthRadius);
    return float2(exp(-h / 8e3), exp(-h / 12e2));
}

float escape(float3 position, float3 direction, float atmosphereRadius, float3 earthCenter)
{
    float3 v = position - earthCenter;
    float b = dot(v, direction);
    float det = b * b - dot(v, v) + atmosphereRadius * atmosphereRadius;
    if (det < 0.)
        return -1.;
    det = sqrt(det);
    float t1 = -b - det;
    float t2 = -b + det;
    return (t1 >= 0.) ? t1 : t2;
}

float2 scatterDepthInt(float3 position, float3 direction, float atmosphericDistance, float steps, SunriseLight light)
{
    float2 depthRMs = float2(0., 0);
    atmosphericDistance /= steps;
    direction *= atmosphericDistance;

    for (float i = 0.; i < steps; ++i)
        depthRMs += densitiesRM(position + direction * i, light);

    return depthRMs * atmosphericDistance;
}

float3 applySunriseLighting(float3 position, float3 direction, float atmosphericDistance, float3 Lo, SunriseLight light)
{
    float3 bR = float3(58e-7, 135e-7, 331e-7); // Rayleigh scattering coefficient
    float3 bMs = float3(2e-5, 2e-5, 2e-5); // Mie scattering coefficients
    float3 bMe = float3(2e-5, 2e-5, 2e-5) * 1.1;
    float2 totalDepthRM = float2(0., 0);
    float3 I_R = float3(0., 0, 0);
    float3 I_M = float3(0., 0, 0);
    float3 oldDirection = direction;
    atmosphericDistance /= 16.0;
    direction *= atmosphericDistance;

    for (float i = 0.; i < 16.0; ++i)
    {
        float3 currentPosition = position + direction * i;
        float2 dRM = densitiesRM(currentPosition, light) * atmosphericDistance;
        totalDepthRM += dRM;
        float2 depthRMsum = totalDepthRM + scatterDepthInt(currentPosition, light.sundir, escape(currentPosition, light.sundir, light.atmosphereRadius, light.earthCenter), 4., light);
        float3 A = exp(-bR * depthRMsum.x - bMe * depthRMsum.y);
        I_R += A * dRM.x;
        I_M += A * dRM.y;
    }

    float mu = dot(oldDirection, light.sundir);
    return Lo + Lo * exp(-bR * totalDepthRM.x - bMe * totalDepthRM.y)
        + light.sunIntensity * (1. + mu * mu) * (
            I_R * bR * .0597 +
            I_M * bMs * .0196 / pow(1.58 - 1.52 * mu, 1.5));
}


//CUSTOM NODE FUNCTIONS
void addSunriseLight(float time, float4 hitPosition, float3 normal, MaterialParams material, float3 rayDirection, out float3 lightingColor)
{
    SunriseLight sunrise;
    sunrise.sundir = normalize(float3(0.5, 0.4 * (1. + sin(0.5 * time)), -1.));
    sunrise.earthCenter = float3(0., -6360e3, 0.);
    sunrise.earthRadius = 6360e3;
    sunrise.atmosphereRadius = 6380e3;
    sunrise.sunIntensity = 10.0;
    
    float atmosphereDist = escape(hitPosition.xyz, rayDirection, sunrise.atmosphereRadius, sunrise.earthCenter);
    float3 lightColor = applySunriseLighting(hitPosition.xyz, rayDirection, atmosphereDist, float3(0, 0, 0), sunrise);
        
    if (hitPosition.w > _raymarchStoppingCriterium)
    {
        lightingColor = lightColor;
        return;
    }
        
    float3 lightDirection = sunrise.sundir;
    float3 viewDirection = normalize(_rayOrigin - hitPosition.xyz);
    float3 reflectedDirection = reflect(-lightDirection, normal);
    
    float3 ambientColor = float3(0, 0, 0);

    float diffuseValue = max(dot(normal, lightDirection), 0.0);
    float specularValue = pow(max(dot(reflectedDirection, viewDirection), 0.0), material.shininess);
    
    float3 diffuseColor = diffuseValue * (0.5 * material.baseColor + 0.5 * lightColor);
    float3 specularColor = specularValue * material.specularColor * material.specularStrength;
        
    lightingColor = ambientColor + diffuseColor + specularColor;
}



struct LightingContext
{
    float3 position; // World-space fragment position
    float3 normal; // Normal at the surface point (normalized)
    float3 viewDir; // Direction from surface to camera (normalized)
    float3 lightDir; // Direction from surface to light (normalized)
    float3 lightColor; // RGB intensity of the light source
    float3 ambient; // Ambient light contribution
};

void lightingContext(float3 hitPos, float3 rayOrigin, out float3 viewDir, out float3 lightDir, out float3 lightColor, out float3 ambientColor)
{
    viewDir = normalize(rayOrigin - hitPos); // Direction from hit point to camera
    float3 baseLightDir = float3(10.0, 5.0, 5.0); // Light position in world space
    lightDir = normalize(baseLightDir - hitPos);
    lightColor = float3(1.0, 1.0, 1.0); // Light color (white)
    ambientColor = float3(0.1, 0.1, 0.1); // Ambient light color<
}

void applyPhongLighting(float3 hitPos, MaterialParams material, float3 normal, out float3 lightingColor)
{
    float3 viewDir, lightDir, lightColor, ambientColor;
    lightingContext(hitPos, _rayOrigin, viewDir, lightDir, lightColor, ambientColor);
    float diff = max(dot(normal, lightDir), 0.0); // Lambertian diffuse

    float3 R = reflect(-lightDir, normal); // Reflected light direction
    float spec = pow(max(dot(R, viewDir), 0.0), material.shininess); // Phong specular

    float3 diffuse = diff * material.baseColor * lightColor;
    float3 specular = spec * material.specularColor * material.specularStrength;

    lightingColor = ambientColor + diffuse + specular;
}

void applyLambertLighting(float3 hitPos, float3 lightPosition, float3 normal, out float3 lightingColor)
{
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 lightColor = float3(1.0, 1.0, 1.0);
    float3 ambientColor = float3(0.05, 0.05, 0.05);

    float diff = max(dot(normal, lightDir), 0.0);
    float3 diffuse = diff * lightColor;

    lightingColor = ambientColor + diffuse;
}

void applyBlinnPhongLighting(float3 hitPos, float3 lightPosition, float3 normal, MaterialParams material, out
float3 lightingColor)
{
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 lightColor = float3(1.0, 1.0, 1.0);
    float3 ambientColor = float3(0.05, 0.05, 0.05);

    float3 halfVec = normalize(viewDir + lightDir);
    float diff = max(dot(normal, lightDir), 0.0);
    // material.shininess
    float spec = pow(max(dot(normal, halfVec), 0.0), material.shininess);

    float3 colour = material.baseColor;
    float3 diffuse = diff * colour * lightColor;
    float3 specular = spec * material.specularColor * material.specularStrength;

    lightingColor = ambientColor + diffuse + specular;
}

void applyToonLighting(float3 hitPos, float3 lightPosition, float3 normal, MaterialParams material, out
float3 lightingColor)
{
    if (hitPos.z == 0)
    {
        lightingColor = float3(0, 0, 0);
        return;
    }
    
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 lightColor = float3(1.0, 1.0, 1.0);
    float3 ambientColor = float3(0.05, 0.05, 0.05);

    float diff = max(dot(normal, lightDir), 0.0);

    float step1 = 0.3;
    float step2 = 0.6;
    float step3 = 0.9;

    float toonDiff =
        diff > step3 ? 1.0 :
        diff > step2 ? 0.7 :
        diff > step1 ? 0.4 : 0.1;

    float3 baseColor = material.baseColor;
    lightingColor = ambientColor + toonDiff * baseColor * lightColor;
}

void applyRimLighting(float3 hitPos, float3 lightPosition, float3 normal, MaterialParams material, out
float3 lightingColor)
{
    if (hitPos.z == 0)
    {
        lightingColor = float3(0, 0, 0);
        return;
    }
    
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 lightColor = float3(1.0, 1.0, 1.0);
    float3 ambientColor = float3(0.05, 0.05, 0.05);

    float rim = 1.0 - saturate(dot(viewDir, normal));
    rim = pow(rim, 4.0);

    float3 baseColor = material.baseColor;
    lightingColor = ambientColor + baseColor * lightColor + rim * material.specularColor;
}

void applySoftSSLighting(float3 hitPos, float3 lightPosition, float3 normal, MaterialParams material, out
float3 lightingColor)
{
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 lightColor = float3(1.0, 1.0, 1.0);
    float3 ambientColor = float3(0.05, 0.05, 0.05);

    float diff = max(dot(normal, lightDir), 0.0);
    float backLight = max(dot(-normal, lightDir), 0.0);

    float3 baseColor = material.baseColor;
    float3 sssColor = float3(1.0, 0.5, 0.5);

    float3 diffuse = diff * baseColor * lightColor;
    float3 sss = backLight * sssColor * 0.25;

    lightingColor = ambientColor + diffuse + sss;
}

void applyFresnelLighting(float3 hitPos, float3 lightPosition, float3 normal, MaterialParams material, out
float3 lightingColor)
{
    if (hitPos.z == 0)
    {
        lightingColor = float3(0, 0, 0);
        return;
    }
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 lightColor = float3(1.0, 1.0, 1.0);
    float3 ambientColor = float3(0.05, 0.05, 0.05);

    float fresnel = pow(1.0 - saturate(dot(viewDir, normal)), 3.0);
    float rimStrength = 1.2;

    float3 baseColor = material.baseColor;
    lightingColor = ambientColor + baseColor * lightColor + rimStrength * fresnel * material.specularColor;
}

void applyUVGradientLighting(float3 hitPos, float3 lightPosition, float3 normal, float2 _uv, out float3 lightingColor)
{
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 lightColor = float3(1.0, 1.0, 1.0);
    float3 ambientColor = float3(0.1, 0.1, 0.1);

    float diff = max(dot(normal, lightDir), 0.0);
    float3 gradientColor = lerp(float3(0.2, 0.4, 0.9), float3(1.0, 0.6, 0.0), _uv.y);

    lightingColor = ambientColor + diff * gradientColor * lightColor;
}

void applyUVAnisotropicLighting(float3 hitPos, float3 lightPosition, float3 normal, float2 _uv, MaterialParams material, out
float3 lightingColor)
{
    float3 viewDir = normalize(_rayOrigin - hitPos);
    float3 lightDir = normalize(lightPosition - hitPos);
    float3 halfVec = normalize(viewDir + lightDir);

    float angle = _uv.x * 6.2831853; // 2π
    float3 localTangent = float3(cos(angle), sin(angle), 0.0);
    float3 tangent = normalize(localTangent - normal * dot(localTangent, normal));
    float3 bitangent = cross(normal, tangent);

    float TdotH = dot(tangent, halfVec);
    float BdotH = dot(bitangent, halfVec);
    float specAniso = pow(TdotH * TdotH + BdotH * BdotH, 8.0);

    float diff = max(dot(normal, lightDir), 0.0);
    float3 baseColor = material.baseColor;
    float3 ambientColor = float3(0.1, 0.1, 0.1);

    lightingColor = ambientColor + diff * baseColor + specAniso * material.specularColor;
}

#endif